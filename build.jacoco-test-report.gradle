apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

jacoco {
    toolVersion = projectJacocoVersion
}

task configureCoverageReport {
    doFirst {
        def includeAppSrc = false;
        //def includeCoreSrc = false;
        //def includeDatabaseSrc = false;
        def coverageFiles = []
        def coverageSourceDirs = []

        // exclude generated classes from code coverage report because not all generated methods will be used
        def defaultExcludes = ['**/R.class', '**/R$*.class', '**/BuildConfig.class' // generated by android
                               , '**/*_*' // generated by androidannotaions
                               , '**/*InjectAdapter.class', '**/*ModuleAdapter*.class' // generated by dagger
                               , '**/database/provider/*' // generated database handling
                               ]

        // Don't take not existing coverage report files or the report will be empty

        def currentCoverageFile = 'app/build/jacoco/testDebugUnitTest.exec'
        if (file(currentCoverageFile).exists()) {
            println 'found app unit test coverage'
            includeAppSrc = true;
            coverageFiles += currentCoverageFile
        }
        currentCoverageFile = 'appIt/build/outputs/code-coverage/connected/coverage.ec'
        if (file(currentCoverageFile).exists()) {
            println 'found app integration test coverage'
            includeAppSrc = true;
            coverageFiles += currentCoverageFile
        }
        currentCoverageFile = 'appCt/build/jacoco/testDebug.exec'
        if (file(currentCoverageFile).exists()) {
            println 'found app component test coverage'
            includeAppSrc = true;
            coverageFiles += currentCoverageFile
        }

        // Don't include classes from modules we haven't executed
        if (includeAppSrc) {
            coverageSourceDirs += 'app/src/main/java'
            tasks.jacocoTestReport.classDirectories += fileTree(dir: 'app/build/intermediates/classes/debug', excludes: defaultExcludes)
        }

        tasks.jacocoTestReport.additionalSourceDirs = files(coverageSourceDirs)
        tasks.jacocoTestReport.executionData = files(coverageFiles)


        // Send code coverage report to coveralls with coveralls-gradle-plugin. To get the plugin working
        // we need to include the coverageSourceDirs as main sources. But this conflicts with android
        // studio and also with jacocoTestReport task.
        coveralls {
            if (isCi) { // avoid android studio source root conflicts
                sourceSets.main.java.srcDirs += coverageSourceDirs
            }
        }
    }
}

compileJava.enabled = false

tasks.jacocoTestReport.dependsOn tasks.configureCoverageReport
tasks.coveralls.dependsOn tasks.configureCoverageReport

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}